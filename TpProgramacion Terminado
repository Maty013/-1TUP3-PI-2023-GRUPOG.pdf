Proceso TpProgramacion
	Definir AgendaDeTurnos Como caracter
	Definir diasSemana Como caracter
	definir horariosTurnos como caracter
	definir StockVacunas Como entero
	definir Vacunas como caracter 
	definir opciones Como Entero 
	definir CantidadDeTurnos como entero 
	definir Turnos Como Caracter
	definir Subopcion Como Caracter
	CantidadDeTurnos = 0
	
	Dimension StockVacunas[6]   // Cantidad de las vacunas disponibles
 	StockVacunas[0] <- 10 //Neumococo conjugada
	StockVacunas[1] <- 10 //Poliomelitis  (IPV o Salk
	StockVacunas[2] <-10 //Quintuple o pentavalente
	StockVacunas[3] <-10 //Rotavirus 
	StockVacunas[4] <-10 //Meningococo
	StockVacunas[5] <-10 //Triple Viral
	dimension Vacunas[6]
	Vacunas[0] <-"Neumococo conjugada"
	Vacunas[1] <-"Poliomelitis  (IPV o Salk)"
	Vacunas[2] <-"Quintuple (o pentavalente)"
	Vacunas[3] <-"Rotavirus "
	Vacunas[4] <-"Meningococo"
	Vacunas[5] <-"Triple Viral"
 	Dimension DiasSemana[5]
	diasSemana[0] <-"Lunes"
	diasSemana[1] <-"Martes"
	diasSemana[2] <-"Miercoles"
	diasSemana[3] <-"Jueves"
	diasSemana[4] <-"Viernes"
	Dimension horariosTurnos[8]
	horariosTurnos[0] <-"8:00"
	horariosTurnos[1] <-"8:30"
	horariosTurnos[2] <-"9:00"
	horariosTurnos[3] <-"9:30"
	horariosTurnos[4] <-"10:00"
	horariosTurnos[5] <-"10:30"
	horariosTurnos[6] <-"11:00"
	horariosTurnos[7] <-"11:30"
	dimension Turnos[5,8]
	
	
	Dimension AgendaDeTurnos[40,6] //Necesitamos poner 2 array para saber que turnos estan disponibles y para luego poder mostrar la Agenda, 40 son la cantidad de turnos posibles
	// y 6 los datos que necesitamos de cada uno de esos turnos 
	// 0: nombre y apellido ,1: DNI, 2: Edad, 3: Vacuna a aplicar, 3:Dia del turno, 4:horario del turno 5: Vacuna a aplicar
	
	
	Repetir
		subopcion = "" // por las dudas
		Escribir "bienvenido al sistema de gestion de turnos"
		Escribir "------------------------------------------"
		Escribir "seleccione una de las siguientes opciones"
		Escribir " "
		Escribir "1-Reservar turno"
		Escribir "2-buscar paciente"
		Escribir "3-Ver agenda vacunatorio"
		Escribir "4-ordenar y mostrar lista de pacientes"
		Escribir "      a. Por edad"
		Escribir "      b. Por vacuna a aplicar"
		Escribir "5-Listado/s"
		Escribir "      a. Cantidad de turnos otorgados por dia"
		Escribir "      b. Cantidad de vacunas a aplicar por vacuna"
		escribir "6-Para salir del programa"
		Escribir ""
		leer opciones 
		segun opciones Hacer
			1:
				reservarTurno(AgendaDeTurnos, diasSemana, horariosTurnos, Vacunas, StockVacunas, CantidadDeTurnos, Turnos)
			2:
				buscarPaciente(AgendaDeTurnos,CantidadDeTurnos)
			3:
				agendaVacunatorio(Turnos,diasSemana,horariosTurnos)
			4:  
				Escribir "Selecione una opcion de Orden"
				Repetir
					escribir "a. Por Edad"
					Escribir "b. Por Vacunas"
					leer subopcion
				Mientras Que subopcion <> "a" Y subopcion <> "A" Y subopcion <> "b" y subopcion <> "B"
 				segun subopcion hacer
					"a", "A": 
						OrdenarPorEdad(AgendaDeTurnos,CantidadDeTurnos)
					"b", "B":
						OrdenarPorVacunas(AgendaDeTurnos,CantidadDeTurnos)
				FinSegun
			5:
				escribir "Seleccione una opcion para el listado"
				Repetir
					escribir "A. Cantidad De turnos por dia"
					Escribir "B. Cantidad de vacunas a aplicar de cada tipo"
					leer subopcion
				Mientras Que subopcion <> "a" Y subopcion <> "A" Y subopcion <> "b" y subopcion <> "B"
				Segun subopcion Hacer
					"a","A":
						MostrarCantidadDeTurnosPorDia(AgendaDeTurnos,CantidadDeTurnos)
					"b","B":
						MostrarCantidadDeVacunasPorTipo(AgendaDeTurnos,CantidadDeTurnos)
						
				FinSegun
			6:
				escribir "hasta luego"
			De Otro Modo:
				Escribir "Ingrese una de las opciones validas"
		Fin Segun
	Mientras Que opciones <> 6
	
FinProceso

SubProceso reservarTurno(AgendaDeTurnos Por Referencia, diasSemana Por Referencia, horariosTurnos Por Referencia, Vacunas, StockVacunas Por Referencia, CantidadDeTurnos Por Referencia, Turnos Por Referencia)
	definir i, j como entero 
	definir Validador como entero 
	definir dia, horario, vacuna como entero 
	definir DNI, EDAD como caracter 
	
	
	Escribir "Turnos disponibles:"
	para i<-0 hasta 4 con paso 1 Hacer
		Escribir "Turnos disponibles ", diasSemana[i], ":"	
		para j<-0 hasta 7 con paso 1 Hacer
			si Turnos[i,j] = "" Entonces
				Escribir j+1, ". " horariosTurnos[j]
			FinSi
		FinPara
		escribir ""
	FinPara
	
	Repetir
		Repetir
			Escribir "Ingrese el dia y horario del turno a reservar"
			Escribir "dias validos (1-5)"
			leer Dia
			Escribir "horarios validos (1-8)"
			leer horario
		Mientras Que (dia<=0 o dia>=5) o (horario>=8 o horario<=0)
		dia = dia-1  
		horario = horario -1 
		
		si turnos[dia,horario] <> "" entonces 
			escribir "El turno seleccionado ya esta ocupado"
			Escribir "por favor a continuacion selecione un horario disponible"
			escribir ""
			validador = 0
		sino 
			validador = 1
			Turnos[dia,horario] = "ocupado"
		FinSi
	Mientras Que Validador = 0
	
	escribir "Ingrese los datos del paciente:"
	escribir "Nombre y Apellido:"
	leer AgendaDeTurnos[CantidadDeTurnos,0]
	Repetir
		Escribir "Dni:"
		leer DNI
	Mientras Que longitud(DNI) >8 o longitud(DNI) < 6
	AgendaDeTurnos[CantidadDeTurnos,1] = DNI
	repetir
		Escribir "Edad:"
		leer EDAD
	Mientras Que Longitud(EDAD) >= 3
	AgendaDeTurnos[CantidadDeTurnos,2] = EDAD
	
	escribir "Vacunas Disponibles:"
	para i<-0 hasta 5 con paso 1 Hacer
		si StockVacunas[i] > 0 Entonces
			escribir i+1 ". ", Vacunas[i]
		FinSi
	FinPara
	
	escribir "selecione la vacuna a aplicar:"
	leer vacuna
	vacuna = vacuna -1
	
	mientras vacuna<0 o vacuna>5 o StockVacunas[vacuna] = 0
		escribir "la vacuna selecionada no esta disponible"
		Escribir "por favor selecione una disponible"
		leer vacuna
		vacuna = vacuna -1
	FinMientras
	
	AgendaDeTurnos[CantidadDeTurnos,3] = diasSemana[dia]
	AgendaDeTurnos[CantidadDeTurnos,4] = horariosTurnos[horario]
	AgendaDeTurnos[CantidadDeTurnos,5] = Vacunas[vacuna]
	
	
	StockVacunas[vacuna] = StockVacunas[vacuna] -1 
	
	Escribir ""
	escribir "Resumen De la reserva"
	escribir "Nombre y Apellido:",AgendaDeTurnos[CantidadDeTurnos,0]
	escribir "DNI:", AgendaDeTurnos[CantidadDeTurnos,1]
	escribir "Edad:", AgendaDeTurnos[CantidadDeTurnos,2]
	escribir "Vacuna:", AgendaDeTurnos[CantidadDeTurnos,5]
	escribir "Dia:", AgendaDeTurnos[CantidadDeTurnos,3]
	escribir "Horario:", AgendaDeTurnos[CantidadDeTurnos,4]
	escribir ""
	
	CantidadDeTurnos = CantidadDeTurnos +1
	
Finsubproceso

SubProceso buscarPaciente(AgendaDeTurnos Por Referencia,CantidadDeTurnos Por Referencia)
	definir i Como Entero
	definir DNI como caracter
	definir seguir Como Caracter
	
	Repetir
		escribir "Ingrese el DNI del paciente:"
		leer DNI
		
		para i<-0 hasta CantidadDeTurnos - 1 con paso 1 hacer 
			si AgendaDeTurnos[i,1] = DNI Entonces
				Escribir "Nombre y Apellido:", AgendaDeTurnos[i, 0]
				Escribir "DNI:", AgendaDeTurnos[i, 1]
				Escribir "Edad:", AgendaDeTurnos[i, 2]
				Escribir "Vacuna a aplicar:", AgendaDeTurnos[i, 5]
				Escribir "DÃ­a turno:", AgendaDeTurnos[i, 3]
				Escribir "Horario turno:", AgendaDeTurnos[i, 4]
				Escribir ""
			sino 
				escribir "No se encontro paciente con el dni ingresado"
				escribir ""
			FinSi
		FinPara
		Repetir
			escribir "Quiere buscar otro paciente?(si o no)"
			leer seguir
		Mientras Que (seguir <>"si" y seguir <>"Si" y seguir <>"SI" y seguir <>"no" y seguir <>"No" y seguir <>"NO")
		
	Mientras Que seguir = "si"
	
FinSubProceso

SubProceso agendaVacunatorio(Turnos,diasSemana,horariosTurnos)
	
	definir i,j como entero
	escribir "Agenda del vacunatorio:"
	para i<-0 hasta 4 con paso 1 Hacer
		escribir "Dia ",diasSemana[i] 
		para j<-0 hasta 7 con paso 1 Hacer
			si turnos[i,j] = "" Entonces
				escribir "  ", horariosTurnos[j], " Turno Libre"
			SiNo
				escribir "  ", horariosTurnos[j], " Turnos Ocupado"
			FinSi
		FinPara
		escribir ""
	FinPara
	
FinSubProceso
SubProceso OrdenarPorEdad(AgendaDeTurnos, CantidadDeTurnos)
	definir i,j,k como entero
	definir aux como caracter  
	para i<-0 hasta CantidadDeTurnos - 2 con paso 1 Hacer
		para j<-0 hasta CantidadDeTurnos - i -2 con paso 1 Hacer
			si ConvertirANumero(AgendaDeTurnos[j,2]) > ConvertirANumero(AgendaDeTurnos[j+1,2]) Entonces
				para k<-0 hasta 5 con paso 1 hacer 
					aux = AgendaDeTurnos[j,k]
					AgendaDeTurnos[j,k] = AgendaDeTurnos[j +1 ,k]
					AgendaDeTurnos[j +1 ,k] = aux
				FinPara
			FinSi
		FinPara
	FinPara
	
	para i<-0 hasta CantidadDeTurnos - 1 con paso 1 Hacer
		escribir "Nombre Paciente:", AgendaDeTurnos[i,0]
		escribir "Edad:", AgendaDeTurnos[i,2]
		escribir "Vacuna a aplicar:", AgendaDeTurnos[i,5]
		escribir "Dia y Horario:", AgendaDeTurnos[i,3],":", AgendaDeTurnos[i,4]
	FinPara
	escribir ""
	
FinSubProceso

SubProceso OrdenarPorVacunas(AgendaDeTurnos,CantidadDeTurnos)
	definir i,j,k como entero 
	definir menor como entero 
	definir aux como caracter
	
	para i<-0 hasta CantidadDeTurnos - 1 con paso 1 Hacer
		menor = i 
		para j<-i+1 hasta CantidadDeTurnos -1 Hacer
			si AgendaDeTurnos[j,5] < AgendaDeTurnos[menor,5] Entonces
				menor = j
			FinSi
		FinPara
		
		//intercambio los valores de i y menor 
		
		
	FinPara
	
	para i<-0 hasta CantidadDeTurnos - 1 con paso 1 Hacer
		escribir "Vacuna a aplicar:", AgendaDeTurnos[i,5]
		escribir "Nombre Paciente:", AgendaDeTurnos[i,0]
		escribir "Edad:", AgendaDeTurnos[i,2]
		escribir "Dia y Horario:", AgendaDeTurnos[i,3],":", AgendaDeTurnos[i,4]
	FinPara
	escribir ""
	
FinSubProceso

SubProceso MostrarCantidadDeTurnosPorDia(AgendaDeTurnos,CantidadDeTurnos)
	definir i como entero
	definir contadorLunes Como Entero
	definir contadorMartes como entero
	definir contadorMiercoles Como Entero
	Definir contadorJueves Como Entero
	Definir contadorViernes como entero 
	definir Dia Como Caracter
	
	contadorLunes=0
	contadorMartes=0
	contadorMiercoles=0
	contadorJueves=0
	contadorViernes=0
	
	// contar turnos por dia
	para i<-0 hasta CantidadDeTurnos -1 con paso 1 Hacer
		Dia = AgendaDeTurnos[i,3]
		segun Dia hacer
			"Lunes":
				contadorLunes = contadorLunes +1 
			"Martes":
				contadorMartes = contadorMartes +1 
			"Miercoles":
				contadorMiercoles = contadorMiercoles +1 
			"Jueves":
				contadorJueves = contadorJueves +1 
			"Viernes":
				contadorViernes = contadorViernes +1 
		FinSegun
	FinPara
	
	//Mostrar cantidades
	Escribir "CanTidad de turnos otorgados por dia:"
	Escribir "Lunes:", contadorLunes
	Escribir "Martes:", contadorMartes
	Escribir "Miercoles:" contadorMiercoles
	Escribir "Jueves:" contadorJueves
	Escribir "Viernes:" contadorViernes
	Escribir ""
	
FinSubProceso
//SubProceso de contador de vacunas 
SubProceso MostrarCantidadDeVacunasPorTipo(AgendaDeTurnos,CantidadDeTurnos)
	definir i Como Entero
	definir vacuna Como Caracter
	
	definir contadorNeumococo Como Entero
	definir contadorPoliomielitis Como Entero
	definir contadorQuintuple Como Entero
	definir contadorRotavirus Como Entero
	definir contadorMeningococo Como Entero
	definir contadorTripleViral Como Entero
	
	contadorNeumococo=0
	contadorPoliomielitis=0
	contadorQuintuple=0
	contadorRotavirus=0
	contadorMeningococo=0
	contadorTripleViral=0
	
	//contar vacunas
	
	para i<-0 hasta CantidadDeTurnos -1 con paso 1 Hacer
		vacunas = AgendaDeTurnos[i,5]
		
		segun vacuna hacer
			"Neumococo conjugada":
				contadorNeumococo = contadorNeumococo+1
			"Poliomielitis (IPV o Salk)":
				contadorPoliomielitis = contadorPoliomielitis+1
			"Quintuple (o pentavalente)":
				contadorQuintuple = contadorQuintuple+1
			"Rotavirus":
				contadorRotavirus = contadorRotavirus+1
			"Meningococo":
				contadorMeningococo = contadorMeningococo+1
			"Triple Viral":
				contadorTripleViral = contadorTripleViral+1
		FinSegun
	FinPara
	
	//Mostrar 
	
	Escribir "Cantidad de vacunas a aplicar por tipo"
	Escribir "Neumococo conjugada:",contadorNeumococo
	Escribir "Poliomielitis (IPV o Salk):" contadorPoliomielitis
	Escribir "Quintuple (o pentavalente):", contadorQuintuple
	Escribir "Rotavirus:", contadorRotavirus
	Escribir "Meningococo:", contadorMeningococo
	Escribir "Triple Viral:",contadorTripleViral
	Escribir ""
	
FinSubProceso
